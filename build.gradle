//file:noinspection HardCodedStringLiteral
plugins {
    id 'com.android.application' version '8.10.1'
    id 'org.jetbrains.kotlin.android' version '2.1.0'
    id 'org.jetbrains.kotlin.plugin.compose' version '2.1.0'
    id 'org.jetbrains.kotlin.plugin.serialization' version '2.1.0'
    id 'com.android.library' version '8.10.1' apply false
}

def getVersionCode = { ->
    def envCode = System.getenv('VERSION_CODE')
    if (envCode?.trim()) {
        try {
            return Integer.parseInt(envCode.trim())
        } catch (NumberFormatException ignored) {
            project.logger.warn("VERSION_CODE='$envCode' is not a valid integer – falling back to git rev-list.")
        }
    } else {
        project.logger.warn("VERSION_CODE env variable not set – falling back to git rev-list.")
    }

    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-list', '--first-parent', '--count', 'HEAD'
            standardOutput = stdout
        }
        return Integer.parseInt(stdout.toString().trim())
    } catch (ignored) {
        project.logger.lifecycle("Failed to get rev-list count from git! Using default 1.")
        return 1
    }
}

def getVersionName = { ->
    def envName = System.getenv('VERSION_NAME')
    if (envName?.trim()) {
        return envName.trim()
    } else {
        project.logger.warn("VERSION_NAME env variable not set – falling back to git describe.")
    }

    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags', '--always'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    } catch (ignored) {
        project.logger.lifecycle("Failed to get version name tag from git! Using default 0.1.0.")
        return "0.1.0"
    }
}

def getBranch = { ->
    def envName = System.getenv('BRANCH_NAME')
    if (envName?.trim()) {
        return envName.trim()
    } else {
        project.logger.warn("BRANCH_NAME env variable not set – falling back to git branch.")
    }

    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'branch', '--show-current'
            standardOutput = stdout
        }
        def branch = stdout.toString().trim()
        return branch.isEmpty() ? "master" : branch
    } catch (ignored) {
        project.logger.lifecycle("Failed to get branch from git! Using default master.")
        return "master"
    }
}


final def translationsWithoutEngValues = { final String path ->
    final def sourceDir = file(path)
    final def taskNameSuffix = sourceDir.name.capitalize()

    final def destDir = file("$buildDir/generated/res-translations-filtered/${taskNameSuffix}")

    tasks.register("prepareResFilter${taskNameSuffix}", Copy) {
        from(sourceDir) {
            include 'values-*/**'

            filter { String line ->
                line
                // Fix duplicate <string> tags
                        .replaceAll(/(<string [^>]+>)&lt;string [^&]+&gt;(.+)&lt;\/string&gt;<\/string>/, '$1$2</string>')
                // Fix malformed xliff:g tags
                        .replaceAll(/&lt;xliff:g ([^&]+)&gt;([^&]+)&lt;\/xliff:g&gt;/, '<xliff:g $1>$2</xliff:g>')
                // Fix malformed quotes
                        .replaceAll(/&amp;quot;/, '&quot;')
                // Insert xliff binding
                        .replaceAll(/<resources>/, '<resources xmlns:xliff="urn:oasis:names:tc:xliff:document:1.2">')
            }
        }
        into(destDir)
        includeEmptyDirs = false

        doFirst {
            if (destDir.exists()) {
                println "Cleaning old contents of $destDir"
                destDir.deleteDir()
            }
        }
    }

    // Ensure it runs before build
    preBuild.dependsOn("prepareResFilter${taskNameSuffix}")

    destDir
}

tasks.register('updateLocales', Exec) {
    workingDir = file('tools/make-keyboard-text-py')
    commandLine = [
            System.getProperty("os.name").toLowerCase().contains("windows") ? "python" : "python3",
            "src/generate.py"
    ]
}

tasks.register('updateBundleResources', Exec) {
    workingDir = file('java/res-bundle')
    commandLine = [
            System.getProperty("os.name").toLowerCase().contains("windows") ? "python" : "python3",
            "download-bundles.py",
            file('java/res-bundle').absolutePath
    ]
}

tasks.register('updateContributors') {
    def script = file('tools/contributors.py')
    def outputFile = file('java/src/org/futo/inputmethod/latin/uix/settings/pages/credits/Contributors.kt')

    inputs.file script
    outputs.file outputFile

    doLast {
        def buffer = new ByteArrayOutputStream()

        exec {
            environment 'GITHUB_TOKEN', System.getenv('GITHUB_TOKEN') ?: ''
            commandLine System.getProperty("os.name").toLowerCase().contains("windows") ? "python" : "python3", script.absolutePath
            standardOutput = buffer
        }

        outputFile.parentFile.mkdirs()
        outputFile.text = buffer.toString("UTF-8")
        println "Wrote contributors to ${project.relativePath(outputFile)}"
    }
}

preBuild.dependsOn updateLocales

android {
    namespace 'org.futo.inputmethod.latin'

    compileSdk 35

    // Required if using classes in android.test.runner
    useLibrary 'android.test.runner'

    // Required if using classes in android.test.base
    useLibrary 'android.test.base'

    // Required if using classes in android.test.mock
    useLibrary 'android.test.mock'

    defaultConfig {
        minSdk 24
        targetSdk 35
        versionName getVersionName()
        versionCode getVersionCode()

        applicationId 'org.futo.inputmethod.latin'
        testApplicationId 'org.futo.inputmethod.latin.tests'
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = false
    }

    bundle {
        language {
            enableSplit = true
        }
        density {
            enableSplit = true
        }
        abi {
            enableSplit = true
        }
    }

    packaging {
        resources {
            excludes += [
                'META-INF/DEPENDENCIES',
                'META-INF/LICENSE',
                'META-INF/LICENSE.txt',
                'META-INF/license.txt',
                'META-INF/NOTICE',
                'META-INF/NOTICE.txt',
                'META-INF/notice.txt',
                'META-INF/ASL2.0',
                'META-INF/*.kotlin_module',
                'kotlin/**',
                'META-INF/versions/**',
                'DebugProbesKt.bin',
                'kotlin-tooling-metadata.json',
                '**/*.kotlin_metadata',
                '**/*.kotlin_builtins'
            ]
        }
        pickFirst '**/libc++_shared.so'
        pickFirst '**/libjsc.so'
    }

    signingConfigs {
        debug {
            storeFile file("java/shared.keystore")
        }
    }

    final def keystorePropertiesFile = rootProject.file("keystore.properties")
    def releaseSigning = signingConfigs.debug
    def hasKeystoreProps = rootProject.file("keystore.properties").exists()
    def hasKeystoreEnv = System.getenv("ANDROID_KEYSTORE_FILE") != null

    if (hasKeystoreProps || hasKeystoreEnv) {
        releaseSigning = signingConfigs.create("release") {
            if (hasKeystoreProps) {
                final def keystoreProperties = new Properties()
                keystoreProperties.load(new FileInputStream(rootProject.file("keystore.properties")))
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile rootProject.file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
            } else {
                keyAlias System.getenv("ANDROID_KEY_ALIAS")
                keyPassword System.getenv("ANDROID_KEY_PASSWORD")
                storeFile rootProject.file(System.getenv("ANDROID_KEYSTORE_FILE"))
                storePassword System.getenv("ANDROID_STORE_PASSWORD")
            }
        }
    } else {
        project.logger.lifecycle('Signing configuration not found (keystore.properties or Env vars), APK may not be signed')
    }


    final def crashReportPropertiesFile = rootProject.file("crashreporting.properties")
    final def crashReportProperties = new Properties()
    def hasCrashProps = crashReportPropertiesFile.exists()
    def hasCrashEnv = System.getenv("ACRA_ENABLED") != null

    if (hasCrashProps) {
        crashReportProperties.load(new FileInputStream(crashReportPropertiesFile))
    } else if (!hasCrashEnv) {
        project.logger.lifecycle('crashreporting.properties not found and no Env vars, crash reporting will be disabled')
    }


    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            signingConfig signingConfigs.debug
            debuggable true
            
            // Performance optimizations for debug builds
            renderscriptOptimLevel 3
            
            // Check if real AAR files exist
            def hasMozc = file('libs/mozc-release.aar').exists() && file('libs/mozc-release.aar').length() > 100000
            buildConfigField "boolean", "HAS_MOZC", hasMozc.toString()
            buildConfigField "boolean", "ENABLE_PERFORMANCE_MONITORING", "true"
            buildConfigField "boolean", "ENABLE_MEMORY_OPTIMIZATION", "true"
        }
        release {
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.debug
            debuggable false
            zipAlignEnabled true
            
            // Advanced performance optimizations for release
            renderscriptOptimLevel 3
            
            // Proguard optimizations
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            
            // Check if real AAR files exist
            def hasMozc = file('libs/mozc-release.aar').exists() && file('libs/mozc-release.aar').length() > 100000
            buildConfigField "boolean", "HAS_MOZC", hasMozc.toString()
            buildConfigField "boolean", "ENABLE_PERFORMANCE_MONITORING", "false"
            buildConfigField "boolean", "ENABLE_MEMORY_OPTIMIZATION", "true"
        }
    }

    buildTypes.each {
        def acraEnabled = hasCrashProps ? crashReportProperties['acraEnabled'] : (System.getenv("ACRA_ENABLED") ?: "false")
        def acraUrl = hasCrashProps ? crashReportProperties['acraUrl'] : (System.getenv("ACRA_URL") ?: "")
        def acraUser = hasCrashProps ? crashReportProperties['acraUser'] : (System.getenv("ACRA_USER") ?: "")
        def acraPassword = hasCrashProps ? crashReportProperties['acraPassword'] : (System.getenv("ACRA_PASSWORD") ?: "")

        it.buildConfigField "boolean", "ENABLE_ACRA", acraEnabled.toString()
        it.buildConfigField "String", "ACRA_URL", "\"${acraUrl}\""
        it.buildConfigField "String", "ACRA_USER", "\"${acraUser}\""
        it.buildConfigField "String", "ACRA_PASSWORD", "\"${acraPassword}\""

        it.buildConfigField "String", "FUTOPAY_URL", "\"upi://pay?pa=8080535871@paytm&pn=ZoraType+Donation&mc=0000&tid=1234567890&tr=zoratype001&tn=Support+ZoraType+Development&am=6.99&cu=USD\""
        it.buildConfigField "String", "FUTOPAY_PRICE", "\"~\$6.99\""

        it.buildConfigField "String", "GOOGLEPAY_URL", "\"upi://pay?pa=8080535871@paytm&pn=ZoraType+Donation&mc=0000&tid=1234567890&tr=zoratype002&tn=Support+ZoraType+Development&am=11.99&cu=USD\""
        it.buildConfigField "String", "GOOGLEPAY_PRICE", "\"~\$11.99\""
    }

    flavorDimensions "buildType"

    productFlavors {
        // Temporarily disabled to fix CI build issues
        // unstable {
        //     dimension "buildType"
        //     applicationIdSuffix ".unstable"
        //     versionNameSuffix "-unstable"

        //     buildConfigField "boolean", "IS_PLAYSTORE_BUILD", "false"
        //     buildConfigField "boolean", "UPDATE_CHECKING", "true"
        //     buildConfigField "boolean", "UPDATE_CHECKING_NETWORK", "false"
        //     buildConfigField "String", "BRANCH", "\"${getBranch()}\""

        //     buildConfigField "String", "PAYMENT_PRICE", "FUTOPAY_PRICE"
        // }

        // stable {
        //     dimension "buildType"
        //     buildConfigField "boolean", "IS_PLAYSTORE_BUILD", "false"
        //     buildConfigField "boolean", "UPDATE_CHECKING", "true"
        //     buildConfigField "boolean", "UPDATE_CHECKING_NETWORK", "false"
        //     buildConfigField "String", "BRANCH", "\"${getBranch()}\""

        //     buildConfigField "String", "PAYMENT_PRICE", "FUTOPAY_PRICE"
        // }

        playstore {
            dimension "buildType"
            applicationIdSuffix ".playstore"
            versionNameSuffix "-playstore"

            buildConfigField "boolean", "IS_PLAYSTORE_BUILD", "true"
            buildConfigField "boolean", "UPDATE_CHECKING", "false"
            buildConfigField "boolean", "UPDATE_CHECKING_NETWORK", "false"
            buildConfigField "String", "BRANCH", "\"${getBranch()}\""
            isDefault = true

            buildConfigField "String", "PAYMENT_PRICE", "GOOGLEPAY_PRICE"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        main {
            res.srcDirs = [
                    'java/res',
                    translationsWithoutEngValues('translations/core'),
                    translationsWithoutEngValues('translations/core-ign'),
                    'java/res-large'
            ]
            java.srcDirs = ['common/src', 'java/src']
            manifest.srcFile 'java/AndroidManifest.xml'
            assets.srcDirs = ['java/assets']
            
            // Exclude problematic source files if AAR dependencies are missing
            def hasMozc = file('libs/mozc-release.aar').exists() && file('libs/mozc-release.aar').length() > 100000
            def hasVad = file('libs/vad-release.aar').exists() && file('libs/vad-release.aar').length() > 10000
            
            println "MOZC AAR exists: ${file('libs/mozc-release.aar').exists()}, size: ${file('libs/mozc-release.aar').exists() ? file('libs/mozc-release.aar').length() : 0}"
            println "VAD AAR exists: ${file('libs/vad-release.aar').exists()}, size: ${file('libs/vad-release.aar').exists() ? file('libs/vad-release.aar').length() : 0}"
            println "Using MOZC: $hasMozc, Using VAD: $hasVad"
            
            if (!hasMozc) {
                println "Excluding MOZC-dependent files and including stubs"
                java.exclude '**/engine/general/JapaneseIME.kt'
                java.exclude '**/settings/pages/pdict/PersonalDictionaryJa.kt'
                java.exclude '**/SettingsExporter.kt'
            } else {
                println "Including MOZC-dependent files and excluding stubs"
                java.exclude '**/engine/general/JapaneseIMEStub.kt'
                java.exclude '**/settings/pages/pdict/PersonalDictionaryJaStub.kt'
                java.exclude '**/SettingsExporterStub.kt'
            }
            if (!hasVad) {
                println "Excluding VAD-dependent files and including stubs"
                java.exclude '**/actions/VoiceInputAction.kt'
                java.exclude '**/settings/pages/VoiceInput.kt'
            } else {
                println "Including VAD-dependent files and excluding stubs"
                java.exclude '**/actions/VoiceInputActionStub.kt'
                java.exclude '**/settings/pages/VoiceInputStub.kt'
            }
        }

        playstore {
            java.srcDirs = ['common/src', 'java/src', 'java/playstore/java']
            res.srcDirs = ['java/res-bundle']
            manifest.srcFile 'java/playstore/AndroidManifest.xml'
            
            // Exclude problematic source files if AAR dependencies are missing
            def hasMozc = file('libs/mozc-release.aar').exists() && file('libs/mozc-release.aar').length() > 100000
            def hasVad = file('libs/vad-release.aar').exists() && file('libs/vad-release.aar').length() > 10000
            
            if (!hasMozc) {
                java.exclude '**/engine/general/JapaneseIME.kt'
                java.exclude '**/settings/pages/pdict/PersonalDictionaryJa.kt'
                java.exclude '**/SettingsExporter.kt'
            } else {
                java.exclude '**/engine/general/JapaneseIMEStub.kt'
                java.exclude '**/settings/pages/pdict/PersonalDictionaryJaStub.kt'
                java.exclude '**/SettingsExporterStub.kt'
            }
            if (!hasVad) {
                java.exclude '**/actions/VoiceInputAction.kt'
                java.exclude '**/settings/pages/VoiceInput.kt'
            } else {
                java.exclude '**/actions/VoiceInputActionStub.kt'
                java.exclude '**/settings/pages/VoiceInputStub.kt'
            }
        }

        androidTest {
            res.srcDirs = ['tests/res']
            java.srcDirs = ['tests/src']
            manifest.srcFile 'tests/AndroidManifest.xml'
        }
    }

    lint {
        checkReleaseBuilds false
    }

    androidResources {
        noCompress 'dict'
    }

    ndkVersion '28.2.13676358'
    externalNativeBuild {
        cmake {
            path 'native/jni/CMakeLists.txt'
        }
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
        viewBinding true
        mlModelBinding true
        buildConfig true
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.13.1'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.8.4'
    implementation 'androidx.lifecycle:lifecycle-runtime:2.8.4'
    implementation 'androidx.lifecycle:lifecycle-runtime-compose:2.8.4'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:2.8.4'
    implementation 'androidx.activity:activity-compose:1.9.1'
    implementation platform('androidx.compose:compose-bom:2024.06.00')
    implementation 'androidx.compose.ui:ui'
    implementation 'androidx.compose.ui:ui-graphics'
    implementation 'androidx.compose.ui:ui-tooling-preview'
    implementation 'androidx.compose.material3:material3'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.navigation:navigation-compose:2.7.7'

    implementation 'com.google.code.findbugs:jsr305:3.0.2'

    implementation 'androidx.datastore:datastore-preferences:1.1.7'
    implementation 'androidx.autofill:autofill:1.1.0'
    implementation 'androidx.window:window:1.3.0'

    // ACRA crash reporting (only for playstore flavor now)
    // stableImplementation 'ch.acra:acra-mail:5.11.1'
    // stableImplementation 'ch.acra:acra-dialog:5.11.1'
    // unstableImplementation 'ch.acra:acra-mail:5.11.1'
    // unstableImplementation 'ch.acra:acra-dialog:5.11.1'

    implementation 'sh.calvin.reorderable:reorderable:2.2.0'

    //implementation 'com.squareup.okhttp3:okhttp:4.11.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.7.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json-jvm:1.7.1'

    //def work_version = "2.9.0"
    //implementation "androidx.work:work-runtime-ktx:$work_version"
    //implementation "androidx.work:work-runtime:$work_version"

    implementation project(":voiceinput-shared")

    implementation "com.charleskorn.kaml:kaml:0.61.0"

    // For MOZC - only include if real AAR file exists
    implementation 'com.google.protobuf:protobuf-javalite:3.8.0'
    implementation 'com.google.guava:guava:33.4.8-android'
    if (file('libs/mozc-release.aar').exists() && file('libs/mozc-release.aar').length() > 100000) {
        implementation(name:'mozc-release', ext:'aar')
    }
    // End for MOZC

    debugImplementation 'androidx.compose.ui:ui-tooling'
    debugImplementation 'androidx.compose.ui:ui-test-manifest'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation "org.mockito:mockito-core:1.9.5"
    androidTestImplementation 'com.google.dexmaker:dexmaker:1.2'
    androidTestImplementation 'com.google.dexmaker:dexmaker-mockito:1.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test:rules:1.5.0'
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'
    androidTestImplementation 'androidx.annotation:annotation:1.0.0'
}

project.logger.lifecycle("versionCode = ${android.defaultConfig.versionCode}")
project.logger.lifecycle("versionName = ${android.defaultConfig.versionName}")